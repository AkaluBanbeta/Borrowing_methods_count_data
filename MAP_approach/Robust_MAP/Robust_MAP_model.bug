model{  
  for (j in 1:H){
    log(mu_c[j])<-theta_c[j]
    theta_c[j] ~ dnorm(eta,inv_tau2)
    mu.inv_c[j] <- 1/mu_c[j]
  }
  for (i in 1:N0) {
    kmu.inv_c[i]<-k.inv*mu.inv_c[HistStudy[i]]
    lambda_c[i] ~ dgamma(k.inv, kmu.inv_c[i])
    Y00[i] ~ dpois(lambda_c[i])
  }
  inv_tau2 <- pow(tau, -2)
  tau ~ dnorm(0, 1.4)I(0,)  # 1 is calculated from data, based on sigma/2 of hist data
  eta  ~ dnorm(0,1.0E-6)
  for(i in 1:nc){
    Yc[i] ~ dpois(lambda_cc[i])
    lambda_cc[i] ~ dgamma(k.inv, kmu.inv_cc)
  }
  mu_cc  <-  exp(theta_cc) # in Poisson case theta is log_lambda
  wr~dcat(c(0.9,0.1))
  precision[1]<-inv_tau2
  precision[2]<-0.36 #  See important_codes for more given the maximum k=2 now
  theta_cc    ~  dnorm(eta, precision[wr])
  for(i in 1:nt){
    Yt[i] ~ dpois(lambda_t[i])
    lambda_t[i] ~ dgamma(k.inv, kmu.inv_t)
  }     
  kmu.inv_t <- k.inv*mu.inv_t
  kmu.inv_cc <- k.inv*mu.inv_cc
  mu.inv_t <- 1/mu_t
  mu.inv_cc <- 1/mu_cc
  k.inv <- 1/k
  k ~ dexp(0.4) 
  mu_t<-mu_cc*RR
  RR<-exp(beta1)
  beta1~dnorm(0,1.0E-2)
}